//이 프로젝트에서 사용 할 공통 플러그인의 버전을 정의
//apply false란? 현재 루트 플젝에선 사용하지 않음을 의미
//아래 subprojects -> apply plugin 에 똑같이 적용 해야함 (둘중에 하나라도 없음 에러)
plugins {
    id 'org.springframework.boot' version '3.3.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'java'
}

//프로젝트 전체에 사용할 전역 변수 선언
ext {
    springBootVersion = '3.3.5'
    springCloudVersion = '2023.0.3'
}

//모든 플젝이 공유할 기본 설정
allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    //Spring Cloud 라이브러리 버전 관리
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    //모든 프로젝트 공통 테스트 라이브러리
    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // Logging
        implementation 'org.slf4j:slf4j-api:2.0.9'

        implementation 'org.modelmapper:modelmapper:2.1.1'

        implementation 'mysql:mysql-connector-java:8.0.33'

        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

        testImplementation 'org.springframework.security:spring-security-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'

    }

    //테스트 실행 방식 설정
    tasks.named('test') {
        useJUnitPlatform()
    }
}